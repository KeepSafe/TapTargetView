plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.mavenpublish)
}

android {
    namespace 'com.getkeepsafe.taptargetview'
    compileSdk libs.versions.compileSdk.get().toInteger()

    defaultConfig {
        minSdkVersion libs.versions.minSdk.get().toInteger()
    }
}

dependencies {
    api libs.androidx.annotation
    api libs.androidx.appcompat
    implementation libs.androidx.core
}

// build a jar with source files
tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

tasks.register('javadoc', Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

// build a jar with javadoc
tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}
